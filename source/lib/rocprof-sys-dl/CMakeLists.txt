# ------------------------------------------------------------------------------#
#
# rocprof-sys dl library
#
# ------------------------------------------------------------------------------#

set(CMAKE_BUILD_TYPE "Release")
set(CMAKE_SKIP_RPATH OFF)
set(CMAKE_BUILD_RPATH_USE_ORIGIN ON)
set(CMAKE_CXX_VISIBILITY_PRESET "internal")
set(CMAKE_POSITION_INDEPENDENT_CODE ON)

find_package(Threads REQUIRED)

add_library(rocprof-sys-dl-library SHARED)
add_library(rocprof-sys::rocprof-sys-dl-library ALIAS rocprof-sys-dl-library)

target_sources(
    rocprof-sys-dl-library
    PRIVATE ${CMAKE_CURRENT_SOURCE_DIR}/dl.cpp ${CMAKE_CURRENT_SOURCE_DIR}/main.c
            ${CMAKE_CURRENT_SOURCE_DIR}/dl/dl.hpp)
target_include_directories(
    rocprof-sys-dl-library
    PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
           $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/../rocprof-sys-user>
           $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}/../rocprof-sys>
           $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>)
target_link_libraries(
    rocprof-sys-dl-library
    PUBLIC $<BUILD_INTERFACE:${dl_LIBRARY}> $<BUILD_INTERFACE:rocprof-sys::common-library>
           $<BUILD_INTERFACE:rocprof-sys::rocprof-sys-compile-definitions>
           $<BUILD_INTERFACE:Threads::Threads>)

add_target_cxx_flag_if_avail(rocprof-sys-dl-library "-ftls-model=global-dynamic")
add_target_cxx_flag_if_avail(rocprof-sys-dl-library "-g3")

set_target_properties(
    rocprof-sys-dl-library
    PROPERTIES OUTPUT_NAME rocprof-sys-dl
               VERSION ${PROJECT_VERSION}
               SOVERSION ${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}
               BUILD_RPATH "\$ORIGIN"
               INSTALL_RPATH "\$ORIGIN")

rocprof_sys_strip_target(rocprof-sys-dl-library)

install(FILES ${CMAKE_CURRENT_SOURCE_DIR}/dl/dl.hpp
        DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/${PROJECT_NAME}/dl)

install(TARGETS rocprof-sys-dl-library DESTINATION ${CMAKE_INSTALL_LIBDIR})
